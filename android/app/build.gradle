def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

def propertyFileNameMap = [
        dev  : 'key_dev.properties',
        stage: 'key_staging.properties',
        prod : 'key_prod.properties'
]
def propertyMap = [:]

for (fileNameEntry in propertyFileNameMap.entrySet()) {
    def keystorePropertiesFile = rootProject.file(fileNameEntry.value)
    if (keystorePropertiesFile.exists()) {
        def keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
        propertyMap.put(fileNameEntry.key, keystoreProperties)
    }
}

android {
    compileSdkVersion 33

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        checkReleaseBuilds false
    }

    defaultConfig {
        applicationId "so.informed"
        minSdkVersion 24
        targetSdkVersion 33
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        ndk {
            // Make sure to onl build Flutter supported architectures
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86_64'
        }
        
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        releaseDev {
            def keystoreProperties = propertyMap['dev']
            if (keystoreProperties != null) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
                storePassword keystoreProperties['storePassword']
            }
        }

        releaseStage {
            def keystoreProperties = propertyMap['stage']
            if (keystoreProperties != null) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
                storePassword keystoreProperties['storePassword']
            }
        }

        releaseProd {
            def keystoreProperties = propertyMap['prod']
            if (keystoreProperties != null) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }

        profile {
            signingConfig null
        }

        debug {
            signingConfig null
        }
    }

    flavorDimensions "env"

    productFlavors {
        dev {
            dimension "env"

            applicationIdSuffix ".dev"

            resValue "string", "app_name", "informed dev"
            resValue "string", "app_link_host", "localhost"
            resValue "string", "app_link_host_2", "localhost2"

            resValue "string", "onelink_host", "localhost"
            resValue "string", "onelink_custom_host", "localhost"
            resValue "string", "onelink_path_prefix", "/tHNA"

            resValue "string", "facebook_app_id", ""
            resValue "string", "facebook_client_token", ""

            if (propertyMap.containsKey('dev')) {
                signingConfig signingConfigs.releaseDev
            } else {
                signingConfig signingConfigs.debug
            }
        }

        stage {
            dimension "env"

            applicationIdSuffix ".staging"

            resValue "string", "app_name", "informed test"
            resValue "string", "app_link_host", "app.staging.informed.so"
            resValue "string", "app_link_host_2", "www.staging.informed.so"

            resValue "string", "onelink_host", "informed-staging.onelink.me"
            resValue "string", "onelink_custom_host", "get.staging.informed.so"
            resValue "string", "onelink_path_prefix", "/tHNA"

            resValue "string", "facebook_app_id", "1152910382298060"
            resValue "string", "facebook_client_token", "7ad28f772522a2349c4968cb7211e7b4"

            if (propertyMap.containsKey('stage')) {
                signingConfig signingConfigs.releaseStage
            } else {
                signingConfig signingConfigs.debug
            }
        }

        prod {
            dimension "env"

            resValue "string", "app_name", "informed"
            resValue "string", "app_link_host", "app.informed.so"
            resValue "string", "app_link_host_2", "www.informed.so"

            resValue "string", "onelink_host", "informed.onelink.me"
            resValue "string", "onelink_custom_host", "get.informed.so"
            resValue "string", "onelink_path_prefix", "/BHtj"

            resValue "string", "facebook_app_id", "345458011020663"
            resValue "string", "facebook_client_token", "7ad28f772522a2349c4968cb7211e7b4"
            

            if (propertyMap.containsKey('prod')) {
                signingConfig signingConfigs.releaseProd
            } else {
                signingConfig signingConfigs.debug
            }
        }
    }
}

flutter {
    source '../..'
}

dependencies {    
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.google.android.gms:play-services-ads-identifier:18.0.1"

    testImplementation 'junit:junit:4.12'

    // https://developer.android.com/jetpack/androidx/releases/test/#1.2.0
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

apply plugin: 'com.google.gms.google-services'
